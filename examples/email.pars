//  Parser para direcciones de email segun el formato: https://help.xmatters.com/ondemand/trial/valid_email_format.htm
//  (solo tomamos lowercase characters)

def predicate prefixChar = (c) {
    return isNumber(c) || isLowerCase(c);
};

def predicate prefixSpecial = (c) {
    return c == '.' || c == '-' || c == '_';
};

def predicate domainChar = (c) {
    return prefixChar(c) || c == '-';
};

def machine email = <<
    transitions = [ 
        start->prefix       when prefixChar,
        prefix->prefix      when prefixChar,
        prefix->special     when prefixSpecial,
        special->prefix     when prefixChar,
        prefix->domainS     when '@',
        domainS->domain     when domainChar,
        domain->domain      when domainChar,
        domain->dot         when '.',
        dot->dot1           when isLowerCase,
        dot1->final         when isLowerCase,
        final->final        when isLowerCase,
        final->dot          when '.'
    ], 
    initialState = start, 
    finalStates = [final]
>>;

def bool result = true;

print("Checking emails...\n");

// Validos
result = result && parse "person@gmail.com" with email;
result = result && parse "alumno@itba.edu.ar" with email;
result = result && parse "abc-d@mail.com" with email;
result = result && parse "abc.def@mail.com" with email;
result = result && parse "abc_def@mail.com" with email;
result = result && parse "abc.def@mail-archive.com" with email;
if (result) {
    print("Valid emails were accepted!\n");
} else {
    print("Valid emails were rejected!\n");
}

// Invalidos
result = false;
result = result || parse "abc-@mail.com" with email;
result = result || parse ".abc@mail.com" with email;
result = result || parse "abc..def@mail.com" with email;
result = result || parse "abc#def@mail.com" with email;
result = result || parse "abc.def@mail.c" with email;
result = result || parse "abc.def@mail#archive.com" with email;
result = result || parse "abc.def@mail" with email;
result = result || parse "abc.def@mail..com" with email;
if (result) {
    print("Invalid emails were accepted!\n");
} else {
    print("Invalid emails were rejected!\n");
}
